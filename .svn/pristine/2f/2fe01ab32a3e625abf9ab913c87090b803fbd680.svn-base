package org.techfusion.view.mediators {
	
	// Import default mx Classes
	import flash.events.Event;
	
	import mx.collections.ArrayCollection;
	
	import org.puremvc.as3.interfaces.IMediator;
	import org.puremvc.as3.interfaces.INotification;
	import org.puremvc.as3.patterns.mediator.Mediator;
	import org.techfusion.controller.*;
	import org.techfusion.events.LoginViewEvent;
	import org.techfusion.model.proxies.DatabaseProxy;
	import org.techfusion.view.components.LoginView;
	import org.techfusion.view.components.MainView;

	public class LoginViewMediator extends Mediator implements IMediator {
		
		public static const NAME:String		= "LoginViewMediator";
		
		public function LoginViewMediator(viewComponent:Object = null) {
			// Debug
			trace(NAME + " ==> Constructor");
			
			// Pass the viewComponent to the superclass where it will be stored in the inherited viewComponent property
			super(NAME, viewComponent);
			
			// Add EventListener
			loginView.addEventListener(LoginViewEvent.LOGIN, onLoginClick);
		}
		
		// Send Notification OnLogin Click
		private function onLoginClick(loginViewEvent:LoginViewEvent):void {
			// Lock Interface
			loginView.mainLoginWindow.enabled = false;
			
			// Notify Event
			sendNotification(ApplicationFacade.LOGIN, loginViewEvent);
		}
		
		private function get loginView():LoginView {
			return viewComponent as LoginView;
		}
		
		override public function listNotificationInterests():Array {
			return [
				ApplicationFacade.CLOSED,
				ApplicationFacade.IOERROR,
				ApplicationFacade.SECERROR,
				ApplicationFacade.DISCONNECT,
				ApplicationFacade.VALID_LOGIN,
				ApplicationFacade.INVALID_LOGIN,
				ApplicationFacade.DB_INITIALIZED,
				ApplicationFacade.DB_QUERYRETURN
			];
		}
		
		override public function handleNotification(notification:INotification):void {
			
			// Define a credentials object
			var credentials:Object	= new Object();
			
			switch (notification.getName()) {
				case ApplicationFacade.VALID_LOGIN:
					// Store Credentials if Requested
					if(loginView.storeCredentials) {
						// Dispatch Credentials Store Notification
						databaseCredentialsHandler(new Event(ApplicationFacade.DB_SETCREDENTS));
						// Reset Flag
						loginView.storeCredentials = false;
						// Reset Check box
						loginView.chkSaveBox.selected = false;
					}
					// Push on Main View
					loginView.navigator.pushView(MainView);
					break;
				case ApplicationFacade.CLOSED:
					// Change State
					loginView.currentState = "Error";
					// Write Error Message
					loginView.errorLabel.text = "Connection Closed by Remote Host\nWrong Credentials or Manager ACL?!";
					break;
				case ApplicationFacade.INVALID_LOGIN:
					// Change State
					loginView.currentState = "Error";
					// Write Error Message
					loginView.errorLabel.text = "Invalid Username or Password!";
					break;
				case ApplicationFacade.IOERROR:
					// Change State
					loginView.currentState = "Error";
					// Write Error Message
					loginView.errorLabel.text = "Network error: " + notification.getBody().toString();
					break;
				case ApplicationFacade.SECERROR:
					// Change State
					loginView.currentState = "Error";
					// Write Error Message
					loginView.errorLabel.text = "Network error: " + notification.getBody().toString();
					break;
				case ApplicationFacade.DISCONNECT:
					// Pop down to the first view
					// loginView.navigator.popToFirstView();
					// Change State
					loginView.currentState = "Base";
					break;
				case ApplicationFacade.DB_INITIALIZED:
					// Cleanup previuosly stored credentials
					loginView.credentialsDataProvider = new ArrayCollection();
					// Define a dummy credentials object
					credentials = new Object();
					credentials.username	= "";
					credentials.password	= "";
					credentials.server		= "";
					// Add Dummy credential at 0
					loginView.credentialsDataProvider.addItemAt(credentials, 0);
					// Search for stored Credentials
					sendNotification(ApplicationFacade.DBCMD, null, ApplicationFacade.DB_GETCREDENTS);
					break;
				case ApplicationFacade.DB_QUERYRETURN:
					// Get Notification Body
					credentials = notification.getBody();
					
					if(credentials) {
						// Add Entry
						loginView.credentialsDataProvider.addItem(credentials);
						loginView.credentialsDataProvider.refresh();
						loginView.ddlSavedCredentials.selectedIndex = 0;
						
						// Update Form
						loginView.credentialSelect();
						
						// Set Credentials as Saved
						loginView.chkSaveBox.selected = true;
					}
					break;
				default:
					break;
			}
			
			// UnLock Interface
			loginView.mainLoginWindow.enabled = true;
		}
		
		/**
		 * Handle Credentials Save Toggle Action
		 */
		protected function databaseCredentialsHandler(event:Event):void {
			// Define a credentials object
			var credentials:Object	= new Object();
			credentials.username	= loginView.txtUsername.text;
			credentials.password	= loginView.txtPassword.text;
			credentials.server		= loginView.txtServer.text;
			
			switch(event.type) {			
				case ApplicationFacade.DB_SETCREDENTS:
					sendNotification(ApplicationFacade.DBCMD, credentials, ApplicationFacade.DB_SETCREDENTS);
					break;
				case ApplicationFacade.DB_DELCREDENTS:
					sendNotification(ApplicationFacade.DBCMD, credentials, ApplicationFacade.DB_DELCREDENTS);
					break;
			}
		}
		
		override public function onRegister():void {
			// Register Proxy
			if(!facade.hasProxy(DatabaseProxy.NAME)){
				facade.registerProxy(new DatabaseProxy());
			}
			
			// Register Commands
			if(!facade.hasCommand(ApplicationFacade.DBCMD)) {
				facade.registerCommand(ApplicationFacade.DBCMD, DatabaseCommand);
			}

			if(!facade.hasCommand(ApplicationFacade.ASTEVENT)) {
				facade.registerCommand(ApplicationFacade.ASTEVENT, AsteriskCommand);
			}
			
			if(!facade.hasCommand(ApplicationFacade.DISCONNECT)) {
				facade.registerCommand(ApplicationFacade.DISCONNECT, DisconnectCommand);
			}
			
			if(!facade.hasCommand(ApplicationFacade.ONMESSAGE)) {
				facade.registerCommand(ApplicationFacade.ONMESSAGE, OnMessageCommand);
			}

			// Register Event Listener
			loginView.addEventListener(ApplicationFacade.DB_SETCREDENTS, databaseCredentialsHandler);
			loginView.addEventListener(ApplicationFacade.DB_DELCREDENTS, databaseCredentialsHandler);

			// Debug
			trace(NAME + " ==> LoginViewMediator Registered");
		}
		
		override public function onRemove():void {
			// UnRegister Event Listener
			loginView.removeEventListener(ApplicationFacade.DB_DELCREDENTS, databaseCredentialsHandler);
			loginView.removeEventListener(ApplicationFacade.DB_SETCREDENTS, databaseCredentialsHandler);
			
			// UnRegister Commands
			if(facade.hasCommand(ApplicationFacade.ONMESSAGE)) {
				facade.removeCommand(ApplicationFacade.ONMESSAGE);
			}
			
			if(facade.hasCommand(ApplicationFacade.DISCONNECT)) {
				facade.removeCommand(ApplicationFacade.DISCONNECT);
			}

			if(facade.hasCommand(ApplicationFacade.ASTEVENT)) {
				facade.removeCommand(ApplicationFacade.ASTEVENT);
			}
			
			if(facade.hasCommand(ApplicationFacade.DBCMD)) {
				facade.removeCommand(ApplicationFacade.DBCMD);
			}
			
			// UnRegister Proxy
			if(facade.hasProxy(DatabaseProxy.NAME)){
				facade.removeProxy(DatabaseProxy.NAME);
			}
			
			// Debug
			trace(NAME + " ==> LoginViewMediator Unegistered");
		}
	}
}