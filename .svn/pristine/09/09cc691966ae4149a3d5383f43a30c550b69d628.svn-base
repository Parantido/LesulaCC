package org.techfusion.view.mediators {
	
	// Import default mx Classes
	import flash.events.Event;
	import flash.events.TimerEvent;
	import flash.system.Capabilities;
	import flash.utils.Dictionary;
	import flash.utils.Timer;
	
	import mx.events.FlexEvent;
	
	import org.puremvc.as3.interfaces.IMediator;
	import org.puremvc.as3.interfaces.INotification;
	import org.puremvc.as3.patterns.mediator.Mediator;
	import org.techfusion.controller.AsteriskCommand;
	import org.techfusion.controller.DisconnectCommand;
	import org.techfusion.controller.OnMessageCommand;
	import org.techfusion.events.AsteriskEvent;
	import org.techfusion.events.DatabaseViewEvent;
	import org.techfusion.view.components.MainView;
	
	import so.cuo.platform.ad.AdEvent;
	import so.cuo.platform.ad.AdItem;
	import so.cuo.platform.ad.AdManager;
	import so.cuo.platform.ad.AdPosition;
	import so.cuo.platform.ad.AdSize;
	import so.cuo.platform.ad.adapters.AdmobAdapter;
	import so.cuo.platform.ad.adapters.ChartboostAdapter;
	import so.cuo.platform.ad.adapters.IAdAdapter;
	import so.cuo.platform.ad.adapters.InmobiAdapter;
	import so.cuo.platform.iad.IAd;
	
	public class MainViewMediator extends Mediator implements IMediator {
		
		// Mediator Name
		public static const NAME:String		= "MainMediator";
		
		// Manager Pinger
		private var timer:Timer				= null;
		
		// AdMob Engine
		private var adManager:AdManager		= null;
		
		public function MainViewMediator(viewComponent:Object = null) {
			// Debug
			trace(NAME + " ==> Constructor");
			
			// Pass the viewComponent to the superclass where it
			// will be stored in the inherited viewComponent property
			super(NAME, viewComponent);
			
			// Initialize View
			initializeView();
			
			// Add Object Event Listener
			mainView.mainApplicationTabbedView.addEventListener(FlexEvent.VALUE_COMMIT, doingRequests);
			
			// Do Main Tab First Query
			sendNotification(ApplicationFacade.ASTEVENT, null, AsteriskEvent.GETCONFIG);
		}
		
		private function get mainView():MainView {
			return viewComponent as MainView;
		}
		
		override public function listNotificationInterests():Array {
			return [
				ApplicationFacade.PONG,
				ApplicationFacade.CLOSED,
				ApplicationFacade.DISCONNECT,
				ApplicationFacade.DB_SETBOOKMARK_OK,
				ApplicationFacade.DB_GETBOOKMARK_OK
			];
		}
		
		override public function handleNotification(notification:INotification):void {
			switch (notification.getName()) {
				case ApplicationFacade.PONG:
					connectivityMonitorStart();
					break;
				case ApplicationFacade.CLOSED:
				case ApplicationFacade.DISCONNECT:
					// Back to Login View
					mainView.navigator.popToFirstView();
					break;
				case ApplicationFacade.DB_SETBOOKMARK_OK:
					// Clear BookMark Cache
					ApplicationFacade.getInstance()._bookmarks = new Dictionary();
					break;
				case ApplicationFacade.DB_GETBOOKMARK_OK:
					// Retrieve Bookmark Object
					var bookmark:Object = notification.getBody();
					if(!bookmark) return;
					
					// Build Unique BookMark Key
					var bookmark_unique:String = bookmark.bookmark_key + "@" + bookmark.server;
					
					// Store Dict Entry if doesn't exists
					ApplicationFacade.getInstance()._bookmarks[bookmark_unique] = bookmark.bookmark_value;
					break;
				default:
					break;
			}
		}
		
		/**
		 * Execute Action Based on Selected Tabs
		 */
		private function doingRequests(evt:FlexEvent):void {
			switch(mainView.mainApplicationTabbedView.selectedIndex) {
				// Main Peers Tab
				case 0:
					// Request For Peers (SIP/IAX) List
					sendNotification(ApplicationFacade.ASTEVENT, null, AsteriskEvent.GETPEERS);
				break;
				
				// Queues Tab
				case 1:
					// Request For Queues List
					// N.B.: Not needed at all
					// sendNotification(ApplicationFacade.ASTEVENT, null, AsteriskEvent.GETQUEUES);
				break;
			}
		}
		
		/**
		 * Start Connectivity Monitor Cycle Timer
		 */
		protected function connectivityMonitorStart():void {
			// Start Manager Monitoring
			if(timer && timer.running) timer.stop();
			timer = new Timer(10000);
			timer.addEventListener(TimerEvent.TIMER, checkConnectivity);
			timer.start();
		}
		
		/**
		 * Reset e start for max time to wait for pong answer
		 */
		protected function checkConnectivity(event:Event):void {
			// Send Manager Ping Request
			sendNotification(ApplicationFacade.ASTEVENT, null, ApplicationFacade.PING);
			// Reinitialize timer for pong waiting
			timer.removeEventListener(TimerEvent.TIMER, checkConnectivity);
			timer.stop();
			timer = null;
			timer = new Timer(2000);
			timer.addEventListener(TimerEvent.TIMER, pingExpired);
			timer.start();
		}
		
		/**
		 * Ping Expired ... Stop Application
		 */
		protected function pingExpired(event:Event):void {
			// Deinitialize timer and return to first view
			timer.removeEventListener(TimerEvent.TIMER, pingExpired);
			timer.stop();
			mainView.navigator.popToFirstView();
			// Debug
			trace(NAME + " ==> Pong never arrived ... disconnecting!");
		}
		
		/**
		 * Initialize AdMob Engine
		 */
		protected function initAdMobEngine():void {
			trace(NAME + "==> 	Initializing Ad Manager (" +Capabilities.os+ ") ...");
			
			var appKey:String = null;
			
			// Check Platform Type
			if(Capabilities.os.indexOf("Linux") >= 0) {
				appKey = "ca-app-pub-0005046673289888/3933733291";
			} else if(Capabilities.os.indexOf("iPhone") >= 0) {
				appKey = "ca-app-pub-0005046673289888/8363932894";
			}
			
			appKey = "ca-app-pub-0005046673289888/3933733291";
			
			if(!appKey) return;
			
			var list:Vector.<AdItem> = new Vector.<AdItem>();
			list.push(new AdItem(new AdmobAdapter(), 10, appKey, "", 5));
			/* Ad Engine to Cover
				list.push(new AdItem(new InmobiAdapter(), 10, SYS.getInmobiAppID()));
				list.push(new AdItem(new ChartboostAdapter(), 10, SYS.chartboostAppId, SYS.appSignature));
				list.push(new AdItem(new IAdAdapter(),10));
			*/
			
			adManager = AdManager.getInstance();
			adManager.enableTrace = true;
			adManager.configPlatforms(list);
			adManager.showInterstitialOrCache();
			adManager.showBannerAbsolute(AdSize.PHONE_LANDSCAPE, 5, 5);
			
			trace(NAME + "==> 	Ad Manager Initialized!");
		}
		
		/**
		 * Shutdown AdMob Engine
		 */
		protected function shutAdMobEngine():void {
			if(adManager != null) {
				adManager.hideBanner();
				adManager = null;
			}
		}
		
		protected function onBannerFail(event:Event):void {
			trace(event.type);
		}
		
		protected function onAdReceived(event:AdEvent):void {
			if (event.type == AdEvent.onBannerReceive) {
				trace(event.data.width, event.data.height);
			} if (event.type == AdEvent.onInterstitialReceive) {
				//                              trace("flash showInterstitial");
				//                              admob.showInterstitial();
			}
		}
		
		override public function onRegister():void {
			// Initialize View if Needed
			initializeView();
			
			// Sending Initial Configuration Notification
			sendNotification(ApplicationFacade.ASTEVENT, null, AsteriskEvent.DOCONF);
			
			// Requesting Bookmarks
			var credentials:Object = ApplicationFacade.getInstance()._credentials;
			sendNotification(ApplicationFacade.DBCMD, credentials, DatabaseViewEvent.DB_GETBOOKMARK);
			
			// Connectivity Monitor Initialize
			connectivityMonitorStart();
			
			// Initialize AdMob Component
			initAdMobEngine();
			
			// Debug
			trace(NAME + " ==> MainViewMediator Registered");
		}
		
		private function initializeView():void {
			// Register Commands
			if(!facade.hasCommand(ApplicationFacade.ASTEVENT)) {
				facade.registerCommand(ApplicationFacade.ASTEVENT, AsteriskCommand);
			}
			
			if(!facade.hasCommand(ApplicationFacade.DISCONNECT)) {
				facade.registerCommand(ApplicationFacade.DISCONNECT, DisconnectCommand);
			}
			
			if(!facade.hasCommand(ApplicationFacade.ONMESSAGE)) {
				facade.registerCommand(ApplicationFacade.ONMESSAGE, OnMessageCommand);
			}
			
			// Register Mediator
			if(!facade.hasMediator(GrowlNotificationMediator.NAME)) {
				facade.registerMediator(new GrowlNotificationMediator(mainView));
			}
		}
		
		override public function onRemove():void {
			// Deinitialize View if Needed
			deinitializeView();
			
			// Debug
			trace(NAME + " ==> MainViewMediator Unregistered");
		}
		
		private function deinitializeView():void {
			// Shutdown AdMob Engine
			shutAdMobEngine();
			
			// Stop url monitoring
			timer.stop();
			timer = null;
			
			// UnRegister Mediator
			if(facade.hasMediator(GrowlNotificationMediator.NAME)) {
				facade.removeMediator(GrowlNotificationMediator.NAME);
			}
			
			// UnRegister Commands
			if(facade.hasCommand(ApplicationFacade.DISCONNECT)) {
				facade.removeCommand(ApplicationFacade.DISCONNECT);
			}
			
			if(facade.hasCommand(ApplicationFacade.ASTEVENT)) {
				facade.removeCommand(ApplicationFacade.ASTEVENT);
			}
			
			if(facade.hasCommand(ApplicationFacade.ONMESSAGE)) {
				facade.removeCommand(ApplicationFacade.ONMESSAGE);
			}
		}
	}
}
