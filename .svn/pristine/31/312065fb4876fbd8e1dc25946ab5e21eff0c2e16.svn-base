<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark"
		actionBarVisible="true" dataChange="{ viewUpdate(); }"
		viewActivate="{ viewActivate(); }" viewDeactivate="{ viewDeactivate(); }" xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:classed="org.techfusion.view.components.classed.*">
	
	<s:states>
		<s:State name="Calls"/>
		<s:State name="Stats"/>
	</s:states>
	
	<fx:Declarations>
		<s:RadioButtonGroup id="searchType"/>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import mx.charts.chartClasses.Series;
			import mx.charts.series.AreaSeries;
			import mx.collections.ArrayCollection;
			import mx.utils.StringUtil;
			
			import org.puremvc.as3.patterns.facade.Facade;
			import org.techfusion.view.mediators.RightQueuesViewMediator;
			
			[Bindable] public  var callsIndex:Dictionary = new Dictionary();
			[Bindable] public  var queuesIndex:Dictionary = new Dictionary();
			[Bindable] public  var queuesMemberIndex:Dictionary = new Dictionary();
			[Bindable] public  var callsDataProvider:ArrayCollection = new ArrayCollection();
			[Bindable] public  var queuesDataProvider:ArrayCollection = new ArrayCollection();
			[Bindable] public  var queuesMemberDataProvider:ArrayCollection = new ArrayCollection();

			// Retrieve Facade Instance
			private var facade:Facade = ApplicationFacade.getInstance();
			
			// Track Left View Status
			private var leftViewStatus:String = "Queues";
			
			protected function viewActivate():void {
				// Registering Mediators
				if(!facade.hasMediator(RightQueuesViewMediator.NAME)) {
					facade.registerMediator(new RightQueuesViewMediator(this));
					
					// Dispatch Initialization Events
					dispatchEvent(new Event(ApplicationFacade.CREATE));
				}
				
				// Style Main DataGrid Appeareance
				gridCalls.setStyle("borderVisible", false);
				
				// Update by Base Status
				updateDataGrid("Queues");
				
				// Enable dataProvider Filter Function
				queuesDataProvider.filterFunction = statsFilterFunction;
				queuesMemberDataProvider.filterFunction = statsFilterFunction;
				
			}
			
			protected function viewDeactivate():void {
				// Unregistering Mediators
				if(facade.hasMediator(RightQueuesViewMediator.NAME)) {
					facade.removeMediator(RightQueuesViewMediator.NAME);
					// Dispatch Deinitialization Events
					dispatchEvent(new Event(ApplicationFacade.DESTROY));
				}
			}
			
			private function searchDataProvider(evt:Event):void {
				switch(leftViewStatus) {
					case "Queues":
						queuesDataProvider.refresh();
						break;
					case "Agents":
						queuesMemberDataProvider.refresh();
						break;
				}
			}
			
			protected function statsFilterFunction(obj:XML):Boolean {
				if(leftViewStatus == "Queues") {
					if(String(obj.Queue).toLowerCase().indexOf(txtFilter.text.toLowerCase()) > -1) {
						if(chkFilter.selected) { return false; } else { return true; }
					}
				} else if(leftViewStatus == "Agents") {
					if(String(obj.Name).toLowerCase().indexOf(txtFilter.text.toLowerCase()) > -1) {
						if(chkFilter.selected) { return false; } else { return true; }
					}
				}
				
				if(chkFilter.selected) { return true; } else { return false; }
			}
			
			/**
			 * Update right View with data passed by left View
			 */
			protected function viewUpdate():void {
				// Handle data passed by left View
				if(data) {
					// Track Left View Status
					leftViewStatus = String(data.view);
					// Update Dinamic Stats DataGrid
					updateDataGrid(leftViewStatus);
					// Reset Search Field
					if(txtFilter) txtFilter.text = "";
				}
			}
			
			public function seriesToMinute(series:Series, item:Object, fieldName:String):Object {
				if (fieldName == 'yValue') {
					var timeval:Number = 0;
					if(StringUtil.trim(item.LastCall).length > 0) {
						timeval = Number(item.LastCall);
					} else if(StringUtil.trim(item.Holdtime).length > 0) {
						timeval = Number(item.Holdtime);
					}
					
					// Return time in minutes
					var date:Date = new Date(timeval * 1000);
					return(date.minutes);
				}
				
				return 0;
			}    
			
			private function channelToName(item:Object, column:GridColumn):String {
				if(item) {
					
					// Channel RegExs
					var chn:RegExp	= new RegExp("^(.*)\/(.*)@(.*)$");
					var chn2:RegExp	= new RegExp("^(.*)\/(.*)$");
					
					// Data Validation
					var results:Array;
					if(chn.test(item.Name)) {
						results = String(item.Name).split(chn);
						return("Agent: " + results[2]);
					} else if(chn2.test(item.Name)) {
						return("Agent: " + results[2]);
						results = String(item.Name).split(chn2);
					} else {
						return String(item.Name);
					}
				}
				
				return "(Noname)";
			}
			
			protected function updateDataGrid(type:String):void {
				
				// Column Series
				var chartSer:AreaSeries = new AreaSeries();
				var chartSeries:Array = new Array();
				
				// Check Datagrid Type
				switch(type) {
					case "Queues":

						chart.dataProvider = queuesDataProvider;
						horizontalCategoryAxis.categoryField = "Queue";
						chartSeries = new Array();
						
						chartSer.dataFunction = seriesToMinute;
						//chartSer.setStyle("itemRenderer", new ClassFactory(mx.charts.renderers.CircleItemRenderer));
						chartSer.yField = "Holdtime";
						chartSer.displayName = "Display Time";
						chartSeries.push(chartSer);
						chartSer = new AreaSeries();
						chartSer.yField = "TalkTime";
						chartSer.displayName = "Talk Time";
						chartSeries.push(chartSer);
						chartSer = new AreaSeries();
						chartSer.yField = "Completed";
						chartSer.displayName = "Calls Completed";
						chartSeries.push(chartSer);
						chartSer = new AreaSeries();
						chartSer.yField = "Abandoned";
						chartSer.displayName = "Calls Abandoned";
						chartSeries.push(chartSer);
						chartSer = new AreaSeries();
						chartSer.yField = "ServiceLevel";
						chartSer.displayName = "Service Level";
						chartSeries.push(chartSer);
						
						break;
					case "Agents":
						
						chart.dataProvider = queuesMemberDataProvider;
						horizontalCategoryAxis.categoryField = "Name";
						chartSeries = new Array();
						
						chartSer.yField = "Queue";
						chartSer.displayName = "Working Queue";
						chartSeries.push(chartSer);
						chartSer = new AreaSeries();
						chartSer.yField = "CallsTaken";
						chartSer.displayName = "Calls Taken";
						chartSeries.push(chartSer);
						chartSer = new AreaSeries();
						chartSer.dataFunction = seriesToMinute;
						chartSer.yField = "LastCall";
						chartSer.displayName = "Last Call";
						chartSeries.push(chartSer);
						
						break;
				}
				
				// Add Series to Set
				chartSet.series = chartSeries;
				
			}
			
			/**
			 * Switches between member or queue views
			 */
			protected function statsView_changeHandler(event:Event):void {
				switch (currentState) {
					case "Stats":
						currentState = "Calls";
						break;
					case "Calls":
						currentState = "Stats";
						break;
					default:
						currentState = "Calls";
						break;
				}
				
				// Reset Search Text Filter
				txtFilter.text = "";
			}
			
		]]>
	</fx:Script>
	
	<s:titleContent>
		<s:HGroup width="100%" height="60%" horizontalAlign="left" verticalAlign="middle" paddingLeft="10">
			<s:Label fontFamily="Ubuntu" fontSize="14" color="0xFFFFFF" text="{ 'Queued Calls: ' + gridCalls.dataProvider.length }" includeIn="Calls"/>
			<s:CheckBox  id="chkFilter" includeIn="Stats" change="{ searchDataProvider(event); }" fontFamily="Ubuntu-DG"/>
			<s:TextInput width="100%" fontFamily="_sans" id="txtFilter" prompt="Filter:" includeIn="Stats" change="{ searchDataProvider(event); }"/>
			<s:Spacer includeIn="Calls" width="100%"/>
		</s:HGroup>
	</s:titleContent>
	
	<s:actionContent>
		<s:Button icon.Calls="@Embed('/assets/images/stats.png')" fontSize="20"
				  id="viewToggleSwitch" click="{ statsView_changeHandler(event); }"
				  icon.Stats="@Embed('/assets/images/call.png')" fontFamily="Ubuntu-DG"/>
	</s:actionContent>
	
	<s:DataGrid visible="false" includeInLayout="false" visible.Calls="true" includeInLayout.Calls="true" fontFamily="Ubuntu" fontSize="12"
				id="gridCalls" width="100%" height="100%" dataProvider="{ callsDataProvider }">
		<s:columns>
			<s:ArrayList>
				<s:GridColumn dataField="CallerIDNum" headerText="Caller"/>
				<s:GridColumn dataField="Queue" headerText="Queue"/>
				<s:GridColumn dataField="Position" headerText="Position"/>
				<s:GridColumn dataField="Count" headerText="Count"/>
				<s:GridColumn dataField="DateTime" headerText="Time"/>
			</s:ArrayList>
		</s:columns>
	</s:DataGrid>
	
	<s:Group id="emptyMsgBox" left="0" right="0" top="0" bottom="0" alpha=".7" includeIn="Calls"
			 visible="{!callsDataProvider.length}" includeInLayout="{!callsDataProvider.length}">
		
		<s:Rect left="0" right="0" top="0" bottom="0">
			<s:fill>
				<s:SolidColor color="0x000000" />
			</s:fill>
		</s:Rect>
		
		<s:Label text="No queued calls ..." color="0xffffff" verticalCenter="0" horizontalCenter="0" />
	</s:Group>
	
	<!-- Column Charts Begin -->
	<mx:AreaChart visible.Stats="true" includeInLayout.Stats="true" visible="false" includeInLayout="false"
				  id="chart" showDataTips="true" left="0" top="{ chartLegend.height + 5 }" bottom="0" right="0"
				  paddingTop="5" paddingLeft="5" paddingRight="5" paddingBottom="5" alpha="0.9" fontFamily="_sans"
				  click="{ if(chart.showAllDataTips) chart.showAllDataTips = false; else chart.showAllDataTips = true; }">
		
		<mx:fill>
			<s:BitmapFill source="@Embed('/assets/images/bg.png')" fillMode="repeat"/>
		</mx:fill>
		
		<mx:horizontalAxis>
			<mx:CategoryAxis id="horizontalCategoryAxis"/>
		</mx:horizontalAxis>
		
		<mx:series>
			<mx:AreaSet id="chartSet" interactionMode="touch" type="overlaid"/>
		</mx:series>
		
	</mx:AreaChart>
	
	<mx:Legend id="chartLegend" dataProvider="{ chart }" fontFamily="_sans" top="5" right="5" direction="vertical"
			   visible.Stats="true" includeInLayout.Stats="true" visible="false" includeInLayout="false" height="25"/>
	<!-- Column Charts End -->
	
</s:View>
